plugins {
    id 'java'
    id 'groovy'
    id 'idea'
    alias(libs.plugins.axionRelease)
    alias(libs.plugins.nexusPublish)
}

group = 'org.rundeck.plugins'
ext.pluginClassNames='com.rundeck.plugins.aws.S3ResourceModelSource'
ext.pluginName = 'S3 resource Model Source'
ext.pluginDescription = 'Use Amazon S3 to store resources model file.'
ext.publishName = "S3 Resource Model Source ${project.version}"
ext.githubSlug = 'rundeck-plugins/aws-s3-model-source'
ext.developers = [
        [id: 'gschueler', name: 'Greg Schueler', email: 'greg@rundeck.com']
]

ext.rundeckPluginVersion = '1.2'

scmVersion {
    tag {
        prefix = 'v'
        versionSeparator = ''
    }
    ignoreUncommittedChanges = true
}

allprojects {
    project.version = scmVersion.version
    apply from: "${rootDir}/gradle/java.gradle"
}

configurations {
    pluginLibs

    implementation {
        extendsFrom pluginLibs
    }
}

repositories {
    mavenCentral()
    mavenLocal()
}

dependencies {
    implementation(libs.rundeckCore) {
        exclude group: "com.google.guava"
    }
    implementation libs.slf4jApi
    
    pluginLibs(libs.awsSdkS3) {
        exclude group: "com.fasterxml.jackson.core"
        exclude group: "com.fasterxml.jackson.dataformat"
    }
    pluginLibs(libs.awsSdkSts) {
        exclude group: "com.fasterxml.jackson.core"
        exclude group: "com.fasterxml.jackson.dataformat"
    }

    testImplementation libs.bundles.testLibs
}

task copyToLib(type: Copy) {
    into "$buildDir/output/lib"
    from configurations.pluginLibs
}

jar {
    from "$buildDir/output"
    manifest {
        def libList = configurations.pluginLibs.collect{'lib/' + it.name}.join(' ')
        attributes 'Rundeck-Plugin-Name' : pluginName
        attributes 'Rundeck-Plugin-Description' : pluginDescription
        attributes 'Rundeck-Plugin-Rundeck-Compatibility-Version': '2.10.0+'
        attributes 'Rundeck-Plugin-Tags': 'java,resource model,aws,s3'
        attributes 'Rundeck-Plugin-License': 'Apache 2.0'
        attributes 'Rundeck-Plugin-Source-Link': 'https://github.com/rundeck-plugins/aws-s3-model-source'
        attributes 'Rundeck-Plugin-Target-Host-Compatibility': 'all'
        attributes 'Rundeck-Plugin-Author': 'Rundeck, Inc.'
        attributes 'Rundeck-Plugin-Classnames': pluginClassNames
        attributes 'Rundeck-Plugin-File-Version': project.version
        attributes 'Rundeck-Plugin-Version': rundeckPluginVersion
        attributes 'Rundeck-Plugin-Archive': 'true'
        attributes 'Rundeck-Plugin-Libs': "${libList}"
    }
}

//set jar task to depend on copyToLib
jar.dependsOn(copyToLib)

test {
    useJUnitPlatform()
}

nexusPublishing {
    packageGroup = 'org.rundeck.plugins'
    repositories {
        sonatype()
    }
}

apply from: "${rootDir}/gradle/publishing.gradle"