import org.gradle.api.tasks.testing.Test

/*
 * Copyright 2017 Rundeck, Inc. (http://rundeck.com)
 *
 * Licensed under the Apache License, Version 2.0 (the "License");
 * you may not use this file except in compliance with the License.
 * You may obtain a copy of the License at
 *
 *     http://www.apache.org/licenses/LICENSE-2.0
 *
 * Unless required by applicable law or agreed to in writing, software
 * distributed under the License is distributed on an "AS IS" BASIS,
 * WITHOUT WARRANTIES OR CONDITIONS OF ANY KIND, either express or implied.
 * See the License for the specific language governing permissions and
 * limitations under the License.
 */
plugins{
    id 'pl.allegro.tech.build.axion-release' version '1.13.4'
}


ext.rundeckPluginVersion = '1.2'
ext.pluginClassNames=
        'com.rundeck.plugins.aws.S3ResourceModelSource'
ext.pluginName = 'S3 resource Model Source'
ext.pluginDescription = 'Use Amazon S3 to store resources model file.'

apply plugin: 'java'
apply plugin: 'groovy'

sourceCompatibility = 1.8
targetCompatibility = 1.8

repositories {
    mavenCentral()
    mavenLocal()
}

scmVersion {
    tag {
        prefix = 'v'
        versionSeparator = ''
    }
}
version scmVersion.version
configurations {
    pluginLibs

    implementation {
        extendsFrom pluginLibs
    }
}

dependencies {
    implementation group: 'org.rundeck', name: 'rundeck-core', version: '4.13.0-20230515'
    implementation "org.slf4j:slf4j-api:1.7.30"
    pluginLibs ('com.amazonaws:aws-java-sdk-s3:1.12.479') {
        exclude group: "com.fasterxml.jackson.core"
        exclude group: "com.fasterxml.jackson.dataformat"
    }
    pluginLibs ('com.amazonaws:aws-java-sdk-sts:1.11.743') {
        exclude group: "com.fasterxml.jackson.core"
        exclude group: "com.fasterxml.jackson.dataformat"
    }

    testImplementation "org.codehaus.groovy:groovy-all:3.0.9"
    testImplementation "org.spockframework:spock-core:2.0-groovy-3.0"
    testImplementation "cglib:cglib-nodep:2.2.2"
    testImplementation 'org.objenesis:objenesis:1.4'
}

task copyToLib(type: Copy) {
    into "$buildDir/output/lib"
    from configurations.pluginLibs
}

jar {
    from "$buildDir/output"
    manifest {
        def libList = configurations.pluginLibs.collect{'lib/' + it.name}.join(' ')
        attributes 'Rundeck-Plugin-Name' : pluginName
        attributes 'Rundeck-Plugin-Description' : pluginDescription
        attributes 'Rundeck-Plugin-Rundeck-Compatibility-Version': '2.10.0+'
        attributes 'Rundeck-Plugin-Tags': 'java,resource model,aws,s3'
        attributes 'Rundeck-Plugin-License': 'Apache 2.0'
        attributes 'Rundeck-Plugin-Source-Link': 'https://github.com/rundeck-plugins/aws-s3-model-source'
        attributes 'Rundeck-Plugin-Target-Host-Compatibility': 'all'
        attributes 'Rundeck-Plugin-Author': 'Rundeck, Inc.'
        attributes 'Rundeck-Plugin-Classnames': pluginClassNames
        attributes 'Rundeck-Plugin-File-Version': version
        attributes 'Rundeck-Plugin-Version': rundeckPluginVersion
        attributes 'Rundeck-Plugin-Archive': 'true'
        attributes 'Rundeck-Plugin-Libs': "${libList}"
    }
    dependsOn(copyToLib)
}

tasks.withType(Test) {
    useJUnitPlatform()
}